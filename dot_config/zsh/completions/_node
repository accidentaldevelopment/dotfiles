#compdef node

local -i ret=1

_arguments \
  '(-)'{-h,--help}'[print this info and exit]' \
  '(-)'{-v,--version}'[print Node.js version]' \
  '(-e --eval)'{-e,--eval}'[evaluate script]:script:' \
  '(-p --print)'{-p,--print}'[evaluate script and print result]' \
  '(-c --check)'{-c,--check}'[syntax check script without executing]' \
  '(-i --interactive)'{-i,--interactive}'[always enter the REPL even if stdin does not appear to be a terminal]' \
  '(-r --require)'{-r,--require}'[module to preload (option can be repeated)]' \
  '--no-deprecation[silence deprecation warnings]' \
  '--throw-deprecation[throw an exception anytime a deprecated function is used]' \
  '--trace-deprecation[show stack traces on deprecations]' \
  '--trace-sync-io[show stack trace when use of sync IO is detected after the first tick]' \
  '--track-heap-objects[track heap object allocations for heap snapshots]' \
  '--prof-process[process v8 profiler output generated using --prof]' \
  '--v8-options[print v8 command line options]' \
  '--tls-cipher-list[use an alternative default TLS cipher list]:cipher list:' \
  '--icu-data-dir=dir[set ICU data load path to dir]' \
  ${${${${(M)${(@f)"$(node --v8-options)"}:# #--*}/ #}/ ##\(/\[}/%\) #/\]} \
  '*: :_files' \
  && ret=0

return $ret
